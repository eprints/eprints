######################################################################
#
#  Show Last "n" EPrints added.
#
######################################################################
#
#  __COPYRIGHT__
#
# Copyright 2000-2008 University of Southampton. All Rights Reserved.
# 
#  __LICENSE__
#
######################################################################

use EPrints;

use strict;
my $handle = new EPrints::Handle;
exit( 0 ) unless( defined $handle );

my $ds = $handle->get_repository->get_dataset( "archive" );

my $citation = $handle->get_repository->get_conf( "latest_tool_citation" );

my $max = 20;
my $mode = $handle->param( "mode" );
$mode = "default" if( !defined $mode );
my $filters = [];
my $conf = $handle->get_repository->get_conf( "latest_tool_modes", $mode );
my $show_conditions = 0;
if( defined $conf )
{
	foreach my $key (keys %{$conf} )
	{
		$citation = $conf->{"citation"} if( $key eq "citation" );
		if( $key eq "filters" )
		{
			$filters = $conf->{"filters"};
			$show_conditions = 1;
		}
		$max = $conf->{"max"} if( $key eq "max" );
	}
}
push @{$filters}, { meta_fields=>[ 'metadata_visibility' ], value=>'show', match=>'EX', describe=>0 };
my $number_to_get = $max;
my $n = $handle->param( "n" );
if( $n > 0 && $n <= $max )
{
	$number_to_get = $n;
}
my $searchexp = new EPrints::Search(
	allow_blank => 1,
	handle => $handle,
	filters => $filters,
	custom_order => "-datestamp",
	dataset => $ds );
my $title;
if( $show_conditions )
{
	$title = $handle->html_phrase( 
		"cgi/latest_tool:title_matching",
		n => $handle->make_text( $number_to_get ),
		search => $searchexp->render_conditions_description );
}
else
{
	$title = $handle->html_phrase( 
		"cgi/latest_tool:title",
		n => $handle->make_text( $number_to_get ) );
}

my $results = $searchexp->perform_search();
my $format = $handle->param( "output" );
if( defined $format )
{
	my @plugins = $handle->plugin_list(
		type=>"Export",
		can_accept=>"list/eprint",
		is_visible=>"all" );
		
	my $ok = 0;
	foreach( @plugins ) { if( $_ eq "Export::$format" ) { $ok = 1; last; } }
	if( $ok ) 
	{
		my $plugin = $handle->plugin( "Export::".$format );
		$handle->send_http_header( 
				"content_type" => $plugin->param( 'mimetype' ) );
		print $results->export( $format );
	}
	else
	{
		my $page = $handle->html_phrase( 
					"lib/searchexpression:export_error_format" );
		$handle->build_page( $title, $page, "latest_tool" );
		$handle->send_page();
	}
	
	$results->dispose;
	$handle->terminate;
	exit;
}

my @records = $results->get_records( 0, $number_to_get );
$results->dispose;

my $page = $handle->make_doc_fragment();
my $type = $handle->get_citation_type( $ds, $citation );
my $container;
if( $type eq "table_row" )
{
	$container = $handle->make_element( "table", class=>"ep_latest_tool_list" );
}
else
{
	$container = $handle->make_element( "div", class=>"ep_latest_tool_list" );
}
$page->appendChild( $container );
my $n = 1;
foreach my $item ( @records )
{
	my $row = $item->render_citation_link( 
		$citation,
		n => [$n++, "INTEGER"] );
	if( $type eq "table_row" )
	{
		$container->appendChild( $row );
	}
	else
	{
		my $div = $handle->make_element( "div", class=>"ep_latest_tool_result" );
		$div->appendChild( $row );
		$container->appendChild( $div );
	}
}
$handle->build_page( $title, $page, "latest_tool" );
$handle->send_page();

$handle->terminate;
exit;

use POSIX qw(strftime);
sub RFC822_time
{
	return( strftime( "%a,  %d  %b  %Y  %H:%M:%S  %z",localtime ) );
}
