######################################################################
#
#  EPrints Staff Submission Editing Page
#
######################################################################
#
#  16/02/2000 - Created by Robert Tansley
#  $Id$
#
######################################################################

use EPrints::EPrint;
use EPrints::HTMLRender;
use EPrints::Database;
use EPrints::Session;
use EPrints::User;
use EPrints::SubmissionForm;
use EPrints::MetaField;

use EPrintSite::SiteRoutines;

use strict;

my $session = new EPrints::Session();

# Work out whether Accept or Reject buttons pressed
my $action = $session->{render}->param( "submit" );
my $stage = $session->{render}->param( "stage" );
my $eprint_id = $session->{render}->param( "eprint_id" );

if( defined $stage && $stage eq $EPrints::SubmissionForm::stage_format &&
	 defined $action && $action eq $EPrints::SubmissionForm::action_finished )
{
	# Intercept the verify page, that's what we were doing!
	$session->{render}->redirect( "view_submission?eprintid=$eprint_id" );
}
elsif( defined $action && ( $action eq "Accept" || $action eq "Bounce" ||
	$action eq "Send" || $action eq "Delete" ) )
{
	# Specialist cases, not for Submission Form
	
	

	if( !defined $eprint_id || $eprint_id eq "" )
	{
		$session->{render}->render_error(
			"Come to this page from the <A HREF=\"buffer\">Submission Buffer ".
				"Overview</A>, not directly",
			"buffer",
			"Return to Submission Buffer Overview" );
	}
	else
	{
		my $eprint = new EPrints::EPrint( $session,
	                                  $EPrints::Database::table_buffer,
	                                  $eprint_id );
		if( !defined $eprint )
		{
			$session->{render}->render_error( "Problem retrieving the submission ".
				"with ID=\"$eprint_id\". May be a problem with the database - ".
				"check the logs.\n" );
		}
		elsif( $action eq "Accept" )
		{	
			# Accept button pressed
			&accepted( $session, $eprint );
		}
		elsif( $action eq "Bounce" )
		{
			# Bounce button pressed - get reason
			&bounce_form( $session, 0, $eprint );
		}
		elsif( $action eq "Delete" )
		{
			# Delete button pressed - get reason
			&bounce_form( $session, 1, $eprint );
		}
		elsif( $action eq "Send" )
		{
			# Actually do the bounce
			&bounce( $session, $eprint );
		}
	}
}
else
{
	# Give to edit form
	my $subform = new EPrints::SubmissionForm(
		$session,
		"view_submission?eprintid=$eprint_id",
		0,
		$EPrints::Database::table_buffer );

	$subform->process();
}

$session->terminate();
	
	

sub accepted
{
	my( $session, $eprint ) = @_;

	if( $eprint->archive() )
	{
		# Successfully archived, redirect
		$session->{render}->redirect( "buffer" );
	}
	else
	{
		$session->{render}->render_error(
			"Problem committing submission to archive. Please check the ".
				"logs.",
			"buffer",
			"Return to Submission Buffer Overview" );
	}
}


sub bounce_form
{
	my( $session, $delete, $eprint ) = @_;
	
	print $session->{render}->start_html(
		( $delete ? "Deleting" : "Bouncing" ) . " Submission" );

	print "<P><CENTER>Please enter in the box below the reason for ".
		"bouncing/deleting this submission, and any possible fix. This will be ".
		"e-mailed to the relevant author.</CENTER></P>\n";

	# Make the field
	my $reason_field = new EPrints::MetaField(
		"reason:multitext:20::0:1:1" );
	my $default_reason =
		( $delete ? $EPrintSite::SiteInfo::default_delete_reason
		          : $EPrintSite::SiteInfo::default_bounce_reason );
	my $title = $eprint->short_title();
	$default_reason =~ s/_SUBMISSION_TITLE_/$title/g;
	
	# Render the box, with the default stem
	print "<P><CENTER>";
	print $session->{render}->start_form();
	print $session->{render}->input_field(
		$reason_field,
		$default_reason );
	print "</CENTER></P>\n";

	# Send + Cancel buttons
	print "<P><CENTER>";
	print $session->{render}->submit_buttons(
		[ "Send",
		  $EPrints::SubmissionForm::action_cancel ] );
	print "</CENTER></P>\n";

	# Other state info
	print $session->{render}->hidden_field( "eprint_id", $eprint->{eprintid} );
	print $session->{render}->hidden_field( "delete", $delete );
	print $session->{render}->end_form();
	print $session->{render}->end_html();
}


sub bounce
{
	my( $session, $eprint ) = @_;

	# Get the user's details
	my $user = EPrints::User->new( $session, $eprint->{username} );

	if( !defined $user )
	{
		# Can't find the user
		$session->{render}->render_error( 
			"Can't find user data! Try again later, or check the logs.",
			"buffer",
			"Return to Submission Buffer Overview" );
		return;
	}
	
	my $delete = $session->{render}->param( "delete" );
	my $success = 0;
	
	if( $delete )
	{
		# Delete the submission
		$success = $eprint->remove();
	}
	else
	{
		# Transfer the EPrint back to the user's inbox
		$success = $eprint->transfer( $EPrints::Database::table_inbox );
	}
	
	if( $success )
	{
		# Successfully transferred, mail the user with the reason
		if( EPrints::Mailer::send_mail(
			$user->full_name(),
			$user->{email},
			"Problem with submission",
			$session->{render}->param( "reason" ) ) )
		{	
			# Successfully bounced, redirect
			$session->{render}->redirect( "buffer" );
		}
		else
		{
			# Couldn't mail
			$session->{render}->render_error( 
				"The submission was successfully ".($delete?"deleted":"bounced").
					", but for some reason the user couldn't be mailed. User ID: ".
					"<STRONG>$user->{username}</STRONG>, Mail: <A ".
					"HREF=\"mailto:$user->{email}>$user->{email}</A>.",
				"buffer",
				"Return to Submission Buffer Overview" );
		}
	}
	else
	{
		# Couldn't be bounced at all
		$session->{render}->render_error( 
			"The submission couldn't be ".($delete?"deleted":"bounced").
			" - probably a database error. Check the logs.",
			"buffer",
			"Return to Submission Buffer Overview" );
	}
}
