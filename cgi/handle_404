######################################################################
#
#  EPrints 404 "Document Not Found" Error Handler
#
######################################################################
#
#  __COPYRIGHT__
#
# Copyright 2000-2008 University of Southampton. All Rights Reserved.
# 
#  __LICENSE__
#
######################################################################

use EPrints::Deletion;
use EPrints::Session;
use EPrintSite::SiteInfo;

use strict;

my $session = new EPrints::Session();

# Try and work out if the user is trying to access a deleted document
my $url = $ENV{REDIRECT_URL};

my $was_deleted = 0;

# Look for digits in the path
if( $url =~ m#/(\d+)/(\d\d)/(\d\d)/(\d\d)/# )
{
	# There's a match, looks like we have an ID
	my $guessed_id = $EPrintSite::SiteInfo::eprint_id_stem.$1.$2.$3.$4;

	# Was the eprint deleted?
	my $deletion_record = new EPrints::Deletion( $session, $guessed_id );
	
	if( defined $deletion_record )
	{
		# The user seems to be trying to get to a deleted eprint. 
		print $session->{render}->render_deleted_eprint( $deletion_record );
	}
	else
	{
		# See if the eprint exists.
		my $eprint = new EPrints::EPrint(
			$session,
			$EPrints::Database::table_archive,
			$guessed_id );
		
		if( defined $eprint )
		{
			# Seems to be a slightly malformed access to an existing eprint.
			existing_record( $session, $eprint );
		}
		else
		{
			# Not a deleted or existing eprint: a plain old Document Not Found
			plain_404( $session );
		}
	}
}
else
{
	# No match, can't find any ID. Just a plain old 404.
	plain_404( $session );
}

$session->terminate();



######################################################################
#
# existing_record( $session, $eprint )
#
#  Render an appropriate erro saying that the user seems to be trying
#  to get to a particular eprint, but may have just entered the URL
#  incorrectly.
#
######################################################################

sub existing_record
{
	my( $session, $eprint ) = @_;
	
	print $session->{render}->start_html( 
		$session->{lang}->phrase( "H:doc_not_found" ) );

	print "<P>";
	print $session->{lang}->phrase( "H:file_not_found" );
	print "<CODE>$url</CODE></P>\n";
	print "<P>";
	print $session->{lang}->phrase( "H:looking_for" );
	print "</P>\n";
	print "<P ALIGN=CENTER>";
	print $session->{render}->render_eprint_citation( $eprint, 1, 1 );
	print "</P>\n";
	
	print $session->{render}->end_html();
}


######################################################################
#
# plain_404( $session )
#
#  Render a standard Document Not Found error message.
#
######################################################################

sub plain_404
{
	my( $session ) = @_;

	print $session->{render}->start_html( 
		$session->{lang}->phrase( "H:file_not_found" ) );
	
	my $url = $ENV{REDIRECT_URL};
	print "<P>";
	print $session->{lang}->phrase( "H:file_not_found" );
	print "<CODE>$url</CODE></P>\n";

	print "<P>";	
	print $session->{lang}->phrase( 
		"H:blurb",
		"<a href=\"mailto:$EPrintSite::SiteInfo::admin\">".
		  $session->{lang}->phrase( "H:site_admin" ).
		  "</a>" );
	print "</P>\n";
	
	print $session->{render}->end_html();
}

