######################################################################
#
#  EPrints Set Password (and Create Account)
#
######################################################################
#
#  __COPYRIGHT__
#
# Copyright 2000-2008 University of Southampton. All Rights Reserved.
# 
#  __LICENSE__
#
######################################################################

use EPrints::Session;
use strict;

my $session = new EPrints::Session;
Apache::exit( 0 ) unless( defined $session );

my( $page, $title ) = make_page( $session );

$page->appendChild( $session->html_phrase( "general:frontpage_link" ) );

$session->build_page( $title, $page, "set_password" );
$session->send_page();

$session->terminate();

sub make_page
{
	my( $session ) = @_;

	my $page = $session->make_doc_fragment;
	my $websignup = $session->get_archive()->get_conf("allow_web_signup");
	my $mlangopt = $session->get_archive()->get_conf("multi_language_options");

	my $title;
	if( $websignup )
	{
		$title = $session->html_phrase( "cgi/set_password:password_title" );
	}
	else
	{
		$title = $session->html_phrase( "cgi/set_password:password_changeonlytitle" );
	}
	my $errtitle = $session->html_phrase( "cgi/set_password:error_title" );

	my $user_ds = $session->get_archive()->get_dataset( "user" );

	my $f_email = $user_ds->get_field( "email" )->clone();
	$f_email->set_property( "confid" , "setpass" );

	my $f_newpass = $user_ds->get_field( "newpassword" )->clone();
	$f_newpass->set_property( "confid" , "setpass" );

	my $f_username = $user_ds->get_field( "username" )->clone();
	$f_username->set_property( "confid" , "setpass" );

	my $f_lang = $user_ds->get_field( "lang" )->clone();
	$f_lang->set_property( "confid" , "setpass" );

	if( !$session->have_parameters() )
	{
		if( $websignup )
		{
			$page->appendChild( $session->html_phrase( "cgi/set_password:intro" ) );
		}
		$page->appendChild( $session->html_phrase( "cgi/set_password:pchange" ) );

		my $fields = [ $f_email, $f_newpass ];

		if( $websignup )
		{
			push @{$fields},$f_username;

			if( $mlangopt )
			{
				push @{$fields},$f_lang;
			}
		}
	
		$page->appendChild( $session->render_input_form(
			fields=>$fields,
			values=>{
				lang => $session->get_langid()
			},
			show_help=>1,
			default_action=>"submit",
			buttons=>{
				submit=>$session->phrase( "cgi/set_password:action_submit" )
			},
			dest=>"set_password" ) );

		return( $page, $title );
	}

	# Process the form.
	my $email = $f_email->form_value( $session );
	my $lang;
	if( $mlangopt )
	{
		$lang = $f_lang->form_value( $session );
	}
	else
	{
		$lang = $session->get_langid();
	}
	my $newpassword = $f_newpass->form_value( $session );
	my $username = $f_username->form_value( $session );
	my $user = EPrints::User::user_with_email( $session, $email );

	if( !EPrints::Utils::is_set( $email ) )
	{
		$page->appendChild( $session->html_phrase( 
			"cgi/set_password:no_email" ) );
		return( $page, $errtitle );
	}
	if( !defined $user && $websignup )
	{
		if( !EPrints::Utils::is_set( $username ) )
		{
			$page->appendChild( $session->html_phrase( 
				"cgi/set_password:no_username" ) );
			return( $page, $errtitle );
		}
		if( defined EPrints::User::user_with_username( $session, $username ) )
		{
			$page->appendChild( $session->html_phrase( 
				"cgi/set_password:username_in_use",
                        	username=>$session->make_text( $username ) ) );
			return( $page, $errtitle );
		}
	
		$user = EPrints::User::create_user( 
				$session, 
				$session->get_archive()->get_conf( "default_user_type" ) ); 
		$user->set_value( "email", $email );
		$user->set_value( "lang", $lang );
		$user->set_value( "username", $username );
		$page->appendChild( $session->html_phrase( 
			"cgi/set_password:created_new_user",
			email=>$session->make_text( $email ) ) );
	}

	if( !$user->has_priv( "set-password" ) )
	{
		$page->appendChild( $session->html_phrase( 
			"cgi/set_password:no_priv",
			email=>$session->make_text( $email ) ) );
		return( $page, $errtitle );
	}

	if( !defined $newpassword || $newpassword eq "" )
	{
		$page->appendChild( $session->html_phrase( "cgi/set_password:no_password" ) );
		return( $page, $errtitle );
	}

	$user->set_value( "newpassword", $newpassword );
	my $pin = sprintf( "%04X%04X%04X%04X",int rand 0xffff,int rand 0xffff,int rand 0xffff,int rand 0xffff );
	$user->set_value( "newemail", undef );
	$user->set_value( "pin", $pin );
	$user->set_value( "pinsettime", time() );
	$user->commit();
	$user->mail( 
		"cgi/set_password:account",
		$session->html_phrase( "mail_password_pin", confirmurl => $session->make_text( $session->get_archive()->get_conf( "perl_url" )."/confirm?userid=".$user->get_value( "userid" )."&pin=".$pin ) ) );
	$page->appendChild( $session->html_phrase( 
		"cgi/set_password:mail_sent",
		email=>$session->make_text( $email ) ) );
	return( $page, $title );
}

