######################################################################
#
#  EPrints Set Password (and Create Account)
#
######################################################################
#
#  __COPYRIGHT__
#
# Copyright 2000-2008 University of Southampton. All Rights Reserved.
# 
#  __LICENSE__
#
######################################################################

use EPrints::Session;
use strict;

my $session = new EPrints::Session;
Apache::exit( 0 ) unless( defined $session );

my( $page ) = make_page( $session );

$page->appendChild( $session->html_phrase( "cgi/set_password:return" ) );

$session->build_page( 
	$session->phrase( "cgi/set_password:password_title" ),
	$page );
$session->send_page();

$session->terminate();

sub make_page
{
	my( $session ) = @_;

	my $page = $session->make_doc_fragment;

	my $user_ds = $session->get_archive()->get_dataset( "user" );

	if( !$session->have_parameters() )
	{
		$page->appendChild( $session->html_phrase( "cgi/set_password:intro" ) );
	
		$page->appendChild( $session->render_input_form(
			[
				$user_ds->get_field( "email" ),
				$user_ds->get_field( "newpassword" ),
				$user_ds->get_field( "lang" )
			],
			{
				lang => $session->get_langid()
			},
			0,#names
			1,#help
			{
				submit=>$session->phrase( "cgi/set_password:action_submit" )
			},
			{},
			{},
			"set_password" ) );
		return $page;
	}

	# Process the form.
	my $email = $user_ds->get_field( "email" )->form_value( $session );
	my $lang = $user_ds->get_field( "lang" )->form_value( $session );
	my $newpassword = $user_ds->get_field( "newpassword" )->form_value( $session );
	my $user = EPrints::User::user_with_email( $session, $email );

	if( !defined $user )
	{
		$user = EPrints::User::create_user( 
				$session, 
				$session->get_archive()->get_conf( "default_user_type" ) ); 
		$user->set_value( "email", $email );
		$user->set_value( "lang", $lang );
		$page->appendChild( $session->html_phrase( 
			"cgi/set_password:created_new_user",
			email=>$session->make_text( $email ) ) );
	}

	if( !$user->has_priv( "set-password" ) )
	{
		$page->appendChild( $session->html_phrase( 
			"cgi/set_password:no_priv",
			email=>$session->make_text( $email ) ) );
		return $page;
	}

	if( !defined $newpassword || $newpassword eq "" )
	{
		$page->appendChild( $session->html_phrase( "cgi/set_password:no_password" ) );
		return $page;
	}

	$user->set_value( "newpassword", $newpassword );
	my $pin = sprintf( "%04X%04X%04X%04X",int rand 0xffff,int rand 0xffff,int rand 0xffff,int rand 0xffff );
	$user->set_value( "newemail", undef );
	$user->set_value( "pin", $pin );
	$user->set_value( "pinsettime", time() );
	$user->commit();
	$user->mail( 
		"cgi/set_password:account",
		"mail_password_pin",
		confirmurl => $session->make_text( $session->get_archive()->get_conf( "server_perl_root" )."/confirm?userid=".$user->get_value( "userid" )."&pin=".$pin ) );
	$page->appendChild( $session->html_phrase( 
		"cgi/set_password:mail_sent",
		email=>$session->make_text( $email ) ) );
	return $page;
}

