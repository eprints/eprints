#!/usr/bin/perl
use EPrints;
use Data::Dumper;

use strict;
my $handle = EPrints->get_handle();
exit( 0 ) unless( defined $handle );

my $base_url = $handle->get_repository->get_conf( "perl_url" )."/rest";

my $path_info = $handle->get_request->path_info;
if( $path_info eq "" )
{
	$handle->redirect( $base_url.'/' );
}
elsif( ! $path_info =~ m/^\// )
{
	error( $handle, 404, "WTF?: $path_info" );
}
else
{
	my @path = split( '/', $path_info );
	shift @path;
	if( $path_info =~ m/\/$/ ) { push @path, ""; }
	serve_top_level( $handle, @path );
}

$handle->terminate;

exit;


##############################

sub serve_top_level
{
	my( $handle, @path ) = @_;

	# /
	if( @path == 1 && $path[0] eq "" )
	{
		$handle->send_http_header( "content_type"=>"text/html" );
		my @ds_ids = $handle->get_repository->get_dataset_ids;
		print "<ul>\n";
		foreach my $ds_id ( @ds_ids )
		{
			my $dataset = $handle->get_repository->get_dataset( $ds_id );
			next if $dataset->confid ne $ds_id;
			print "<li><a href='$ds_id/'>$ds_id</a></li>\n";
		}
		print "</ul>\n";
		return;
	}

	# /eprint
	if( @path == 1 )
	{
		$handle->redirect( $base_url.$path_info."/" );
		return;
	}

	# /eprint/
	my $dataset_id = shift @path;

	my $dataset = $handle->get_repository->get_dataset( $dataset_id ); 
	if( !defined $dataset )
	{
		error( $handle, 404, "Not Found" );
		return;
	}


	return serve_dataset( $handle, $dataset, @path );
}
	
sub serve_dataset
{
	my( $handle, $dataset, @path ) = @_;

	my $file = shift @path;

	if( scalar @path == 0 )
	{
		# /
		if( $file eq "" )
		{
			return unless allowed_methods( $handle, "GET" );
			my $ul = "<ul>";
			foreach my $id ( sort { $a <=> $b } @{$dataset->get_item_ids( $handle )} )
			{
				$ul .= "<li><a href='$id/'>$id/</a></li>\n";
				$ul .= "<li><a href='$id.xml'>$id.xml</a></li>\n";
			}
			$ul .= "</ul>";
			$handle->send_http_header( "content_type"=>"text/html" );
			print render_html( $ul );
			return;
		}

		# /23
		if( $file =~ m/^\d+$/ )
		{
			$handle->redirect( $base_url.$path_info."/" );
			return;
		}

		my( $item_id, $format) = split( /\./, $file, 2 );
		my $object = $dataset->get_object( $handle, $item_id );
		if( !defined $object )
		{
			error( $handle, 404, "Object $item_id Not Found" );
			return;
		}

		if( defined $format && $format eq "xml" )
		{
			return unless allowed_methods( $handle, "GET","PUT" );
			my $method = $ENV{REQUEST_METHOD};
			if( $method eq "GET" )
			{
				get_dataobj_xml( $handle, $object );
			}
			if( $method eq "PUT" )
			{
				put_dataobj_xml( $handle, $object );
			}
			return;
		}

		error( $handle, 404, "Not Found" );
		return;
	}

	# /23/	
	my $object = $dataset->get_object( $handle, $file );
	if( !defined $object )
	{
		error( $handle, 404, "Object $file Not Found" );
		return;
	}

	return serve_dataobj( $handle, $object, @path );
}


sub serve_dataobj
{
	my( $handle, $object, @path ) = @_;

	return unless allow_priv( $object->get_dataset->confid."/view", $handle, $object );

	my $file = shift @path;
	
	if( scalar @path == 0 )
	{
		# /
		if( $file eq "" )
		{
			return unless allowed_methods( $handle, "GET" );
			my $c = "<ul>";
			foreach my $field ( $object->get_dataset->get_fields )
			{
				my $name = $field->get_name;
				$c.="<li><a href='$name.xml'>$name.xml</a></li>";
				if( $field->get_property( "multiple" )
		 		 || $field->is_type( "compound","subobject","name" ) )
				{
					$c.="<li><a href='$name/'>$name/</a></li>";
				}
				else
				{
					$c.="<li><a href='$name.txt'>$name.txt</a></li>";
				}
			}
			$c.= "</ul>";
			$handle->send_http_header( "content_type"=>"text/html" );
			print render_html( $c );
			return;
		}

		my( $field_name, $format) = split( /\./, $file, 2 );
		my $field = $object->get_dataset->get_field( $field_name );	
		if( !defined $field )
		{
			error( $handle, 404, "Field Not Found: $field_name" );
			return;
		}

		if( $field->get_property( "multiple" )
 		 || $field->is_type( "compound","subobject","name" ) )
		{
			if( $file eq $field_name ) # ie no . in it
			{
				# /title
				$handle->redirect( $base_url.$path_info."/" );
				return;
			}
		}
		else
		{
			if( $format eq "txt" )
			{
				# /title.txt
				return unless allowed_methods( $handle, "GET","PUT" );
				my $method = $ENV{REQUEST_METHOD};
				if( $method eq "GET" )
				{
					get_field_txt( $handle, $object, $field );
				}
				if( $method eq "PUT" )
				{
					put_field_txt( $handle, $object, $field );
				}
				return;
			}	
		}

		if( $format eq "xml" )
		{
			# /title.xml
			return unless allowed_methods( $handle, "GET","PUT" );
			my $method = $ENV{REQUEST_METHOD};
			if( $method eq "GET" )
			{
				get_field_xml( $handle, $object, $field );
			}
			if( $method eq "PUT" )
			{
				put_field_xml( $handle, $object, $field );
			}
			return;
		}	

		error( $handle, 404, "Not Found" );
		return;
	}

	# /title/
	my $field = $object->get_dataset->get_field( $file );	
	if( !defined $field )
	{
		error( $handle, 404, "Field Not Found: $file" );
		return;
	}

	my $v = $object->get_value( $file );

	return serve_field( $handle, $object, $field, $v, @path );
}


sub serve_field
{
	my( $handle, $object, $field, $value, @path ) = @_;

	if( !$field->get_property( "multiple" ) )
	{
		return serve_field_single( $handle, $object, $field, $value, @path );
	}

	my $file = shift @path;

	if( scalar @path == 0 )
	{
		# /
		if( $file eq "" )
		{
			return unless allowed_methods( $handle, "GET" );
			my $c = "<ul>";
			$c.= "<li><a href='size.txt'>size.txt</a></li>";
			for( my $i=0; $i<scalar @{$value}; ++$i )
			{
				my $n = $i + 1;
 				if( $field->is_type( "subobject" ) )
				{
					$c.="<li><a href='$n/'>$n/</a></li>";
					$c.="<li><a href='$n.xml'>$n.xml/</a></li>";
				}
 				elsif( $field->is_type( "compound","name" ) )
				{
					$c.="<li><a href='$n/'>$n/</a></li>";
				}
				else
				{
					$c.="<li><a href='$n.txt'>$n.txt</a></li>";
				}
			}
			$c.="</ul>";
			$handle->send_http_header( "content_type"=>"text/html" );
			print render_html( $c );
			return;
		}

		if( $file eq "size.txt" )
		{
			return unless allowed_methods( $handle, "GET" );
			$handle->send_http_header( "content_type"=>"text/plain" );
			print scalar( @{$value} );
			return;
		}
	
		my( $n, $format ) = split( /\./, $file, 2 );

		if( $format eq "xml" && $field->is_type( "subobject" ) )
		{
			return unless allowed_methods( $handle, "GET","PUT" );
			$handle->send_http_header( "content_type"=>"text/xml" );
			print "<?xml version=\"1.0\" encoding=\"UTF-8\" ?>\n";
			print $object->export( "XML" );
			return;
		}

		if( !EPrints::Utils::is_set( $value->[$n-1] ) )
		{
			error( $handle, 404, "Not Found" );
			return;
		}

		if( $field->is_type( "compound","subobject","name" ) )
		{
			if( $file eq $n ) # ie no . in it
			{
				# /3
				$handle->redirect( $base_url.$path_info."/" );
				return;
			}
		}
		else
		{
			if( $format eq "txt" )
			{
				# /3.txt
				return unless allowed_methods( $handle, "GET" );
				$handle->send_http_header( "content_type"=>"text/plain" );
				print $value->[$n-1];
				return;
			}
		}
		error( $handle, 404, "Not Found" );
		return;
	}

	if( $file !~ m/^\d+$/ )
	{
		error( $handle, 404, "Not Found" );
		return;
	}	

	my $i = $file-1;
	if( !EPrints::Utils::is_set( $value->[$i] ) )
	{
		error( $handle, 404, "Not Found" );
		return;
	}

	# /3/
	return serve_field_single( $handle, $object, $field, $value->[$i], @path );
}

sub serve_field_single
{
	my( $handle, $object, $field, $value, @path ) = @_;

	if( $field->is_type( "subobject" ) )
	{
		return serve_subobject( $handle, $object, $field, $value, @path );
	}
	if( $field->is_type( "compound" ) )
	{
		return serve_compound( $handle, $object, $field, $value, @path );
	}
	if( $field->is_type( "name" ) )
	{
		return serve_name( $handle, $object, $field, $value, @path );
	}
	
	error( $handle, 500, "Unknown field type in serve_field_single()" );
}

sub serve_subobject
{
	my( $handle, $object, $field, $value, @path ) = @_;

	my $ds = $handle->get_repository->get_dataset( $field->get_property('datasetid') );

	return serve_dataobj( $handle, $value, @path );
}


sub serve_compound
{
	my( $handle, $object, $field, $value, @path ) = @_;

	my $file = shift @path;

	my $f = $field->get_property( "fields_cache" );
	my %fieldname_to_alias = $field->get_fieldname_to_alias;
	my %alias_to_fieldname = $field->get_alias_to_fieldname;

	if( scalar @path == 0 )
	{
		if( $file eq "")
		{
			# /
			return unless allowed_methods( $handle, "GET" );
			my $c = "<ul>";
			foreach my $sub_field ( @{$f} )
			{
				my $fieldname = $sub_field->get_name;
				my $alias = $fieldname_to_alias{$fieldname};
				if( $sub_field->is_type( "compound","subobject","name" ) )
				{
					$c.="<li><a href='$alias/'>$alias/</a></li>";
				}
				else
				{
					$c.="<li><a href='$alias.txt'>$alias.txt</a></li>";
				}
			
			}
			$c .= "</ul>";
			$handle->send_http_header( "content_type"=>"text/html" );
			print render_html( $c );
			return;
		}

		my( $part, $format ) = split( /\./, $file, 2 );

		my $fieldname = $alias_to_fieldname{$part};
		if( !defined $fieldname )
		{
			error( $handle, 404, "Not Found" );
			return;
		}
		my $sub_field = $object->get_dataset->get_field( $fieldname );

		if( $part eq $file )
		{
			# /foo
			$handle->redirect( $base_url.$path_info."/" );
			return;
		}

		# /foo.txt
		if( !$sub_field->is_type( "compound","subobject","name" ) && $format eq "txt" )
		{
			return unless allowed_methods( $handle, "GET" );
			$handle->send_http_header( "content_type"=>"text/plain" );
			print $value->{$part};
			return;
		}

		error( $handle, 404, "Not Found" );
		return;
	}

	my $fieldname = $alias_to_fieldname{$file};
	if( !defined $fieldname )
	{
		error( $handle, 404, "Not Found" );
		return;
	}
	my $sub_field = $object->get_dataset->get_field( $fieldname );

	# /foo/
	if( $sub_field->is_type( "compound","subobject","name" ) )
	{
		return serve_field_single( $handle, $object, $sub_field, $value->{$file}, @path );
	}
	
	error( $handle, 404, "Not Found" );
}

sub serve_name
{
	my( $handle, $object, $field, $value, @path ) = @_;

	my $file = shift @path;

	if( scalar @path == 0 )
	{
		# /
		if( $file eq "" )
		{
			return unless allowed_methods( $handle, "GET" );
			my $c = <<END;
<ul>
<li><a href='honourific.txt'>honourific.txt</a></li>
<li><a href='given.txt'>given.txt</a></li>
<li><a href='family.txt'>family.txt</a></li>
<li><a href='lineage.txt'>lineage.txt</a></li>
</ul>
END
			$handle->send_http_header( "content_type"=>"text/html" );
			print render_html( $c );
			return;
		}

		if( $file =~ m/^(honourific|given|family|lineage)\.txt$/ )
		{
			# /given.txt
			return unless allowed_methods( $handle, "GET" );
			my $v = $value->{$1} || "";
			$handle->send_http_header( "content_type"=>"text/plain" );
			print $v;
			return;
		}
	}

	error( $handle, 404, "Not Found" );
	return;
}

sub error
{
	my( $handle, $code, $msg ) = @_;

	print "$msg\n";
}


sub render_html
{
	my( $html ) = @_;

	return <<END;
<html>
  <head><title>RESTful EPrints Interface</title></head>
  <body>
    <h1>RESTful EPrints Interface</h1>
$html
  </body>
</html>
END
}

sub allowed_methods
{
	my( $handle, @methods ) = @_;

	my $method = $ENV{REQUEST_METHOD};
	if( $method eq "OPTIONS" )
	{
		EPrints::Apache::AnApache::header_out( 
			$handle->get_request,
			"Content-Length" => 0 );
		EPrints::Apache::AnApache::header_out( 
			$handle->get_request,
			"Allow" => "OPTIONS,".join( ",", @methods ) );
		EPrints::Apache::AnApache::send_http_header( $handle->get_request );
		return 0;
	}

	foreach my $m ( @methods ) { return 1 if $m eq $method; }

	EPrints::Apache::AnApache::send_status_line( 
			$handle->get_request,
			501,
			"Method Not Implemented" );
	EPrints::Apache::AnApache::send_http_header( $handle->get_request );

	return 0;
}

sub get_dataobj_xml
{
	my( $handle, $object ) = @_;

	return unless allow_priv( $object->get_dataset->confid."/view", $handle, $object );

	$handle->send_http_header( "content_type"=>"text/xml" );
	print "<?xml version=\"1.0\" encoding=\"UTF-8\" ?>\n";
	print $object->export( "XML" );
}

sub put_dataobj_xml
{
	my( $handle, $object ) = @_;

	return unless allow_priv( $object->get_dataset->confid."/staff/edit", $handle, $object );

	$handle->send_http_header( "content_type"=>"text/plain" );
	print "NOT DONE YET\n";
}

sub get_field_txt
{
	my( $handle, $object, $field ) = @_;

	return unless allow_priv( $object->get_dataset->confid."/view", $handle, $object );

	$handle->send_http_header( "content_type"=>"text/plain" );
	print $object->get_value( $field->get_name );
}

sub get_field_xml
{
	my( $handle, $object, $field ) = @_;

	return unless allow_priv( $object->get_dataset->confid."/view", $handle, $object );

	$handle->send_http_header( "content_type"=>"text/xml" );
	print "<?xml version=\"1.0\" encoding=\"UTF-8\" ?>\n";
	print EPrints::XML::to_string( $field->to_xml( $handle, $object->get_value( $field->get_name ), $object->get_dataset ));
}

sub put_field_xml
{
	my( $handle, $object, $field ) = @_;

	if( $field->get_name eq $object->get_dataset->get_key_field->get_name || $field->get_name eq "rev_number" )
	{
		my $r = $handle->get_request;
		EPrints::Apache::AnApache::send_status_line( $r, 403, "Forbidden" );
	}
	return unless allow_priv( $object->get_dataset->confid."/staff/edit", $handle, $object );

	$handle->send_http_header( "content_type"=>"text/plain" );
	print "NOT DONE YET\n";
}

sub put_field_txt
{
	my( $handle, $object, $field ) = @_;

	if( $field->get_name eq $object->get_dataset->get_key_field->get_name || $field->get_name eq "rev_number" )
	{
		my $r = $handle->get_request;
		EPrints::Apache::AnApache::send_status_line( $r, 403, "Forbidden" );
	}
	return unless allow_priv( $object->get_dataset->confid."/staff/edit", $handle, $object );

	$object->set_value( $field->get_name, $handle->{putdata} );
	$object->commit;
	$handle->send_http_header( "content_type"=>"text/plain" );
	print "OK\n";
}

sub allow_priv
{
	my( $priv, $handle, $object ) = @_;

	if( $priv =~ m/^eprint\// )
	{
		my $status = $object->get_value( "eprint_status" );
		$priv =~ s/^eprint\//eprint\/$status\//;	
	}

	return 1 if( $handle->allow_anybody( $priv ) );
	return 1 if( $priv eq "eprint/archive/view" );

	my $r = $handle->get_request;
	$r->auth_type( "Basic" );
	$r->auth_name( "EPrintsREST" );
	my ($status, $password) = $r->get_basic_auth_pw;
	my $username = $r->user;

	if( !$handle->valid_login( $username, $password ) )
	{
		$r->note_basic_auth_failure;
		EPrints::Apache::AnApache::send_status_line( $r, 401, "Auth Required" );
		return 0;
	}

	my $user = $handle->get_user_with_username( $username );

	if( !defined $user )
	{
		print STDERR "Unknown user.\n";
		return 0;
	}

	if( !$user->allow( $priv, $object ) )
	{
		EPrints::Apache::AnApache::send_status_line( $r, 403, "Forbidden" );
		return 0;
	}

	return 1;
}


