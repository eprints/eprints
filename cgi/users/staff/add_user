######################################################################
#
#  Add a User
#
######################################################################
#
#  __COPYRIGHT__
#
# Copyright 2000-2008 University of Southampton. All Rights Reserved.
# 
#  __LICENSE__
#
######################################################################

use EPrints::Database;
use EPrints::MetaField;
use EPrints::Session;
use EPrints::User;

use strict;

my $session = new EPrints::Session;
Apache::exit( 0 ) unless( defined $session );

# Check the permissions

# Check we have privs
if( !$session->auth_check( "edit-user" ) )
{
	$session->terminate();
	Apache::exit( 0 );
}



my $page = $session->make_doc_fragment();
if( $session->seen_form() )
{
	&process_form( $session , $page );
}
else
{
	&render_form( $session , $page );
}


sub render_form
{
	my( $session , $page ) = @_;

	$page->appendChild( $session->html_phrase( "cgi/users/add_user:enter_username" ) );
	my $userds = $session->get_archive()->get_dataset( "user" );

	$page->appendChild( $session->render_input_form(
		show_names=>1,
		show_help=>1,
		fields=>[
			$userds->get_field( "username" ),
			$userds->get_field( "usertype" )
		],
		buttons=>{ 
			createuser=>$session->phrase( "cgi/users/add_user:create_user" ) 
		} 
	) );
	
	$session->build_page(
		$session->phrase( "cgi/users/add_user:create_user_title" ),
		$page );
	$session->send_page();
}

$session->terminate();

sub process_form
{
	my( $session , $page ) = @_;

	# First ensure something's been entered
	my $candidate_username = $session->param( "username" );

	unless( EPrints::Utils::is_set( $candidate_username ) )
	{
		$page->appendChild(
			$session->html_phrase( "cgi/users/add_user:no_username" ) );
		$page->appendChild( $session->render_ruler() );
		&render_form( $session , $page );
		return;
	}

	if( defined EPrints::User::user_with_username( $session, $candidate_username ) )
	{
		$page->appendChild(
			$session->html_phrase( "cgi/users/add_user:user_exists",
				username=>$session->make_text( $candidate_username ) ) );
		$page->appendChild( $session->render_ruler() );
		&render_form( $session , $page );
		return;
	}


	my $usertype = $session->param( "usertype" );
	if( !defined $usertype )
	{
		$usertype = $session->get_archive()->get_conf( "default_user_type" ); 
	}

	# Attempt to create a new account
	my $new_user = EPrints::User::create_user( $session, $usertype );
	
	unless( defined $new_user )
	{
		$session->render_error(
			$session->html_phrase( "cgi/users/add_user:mk_acc_err" ) );
		return;
	}

	$new_user->set_value( "username", $candidate_username );
	$new_user->commit();

	# Created the user OK, send to edit screen
	$session->redirect(
			"edit_user?username=$candidate_username" );
}

