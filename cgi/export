######################################################################
#
#  EPrints Repository Info Exporter
#
######################################################################
#
#  __COPYRIGHT__
#
# Copyright 2000-2010 University of Southampton. All Rights Reserved.
# 
#  __LICENSE__
#
######################################################################

use EPrints;
use Data::Dumper;

use Digest::MD5;
use EPrints::Sword::Utils;
 
use Apache2::RequestRec ();
use Apache2::RequestIO ();

use strict;
my $repository = EPrints->new->current_repository;
exit( 0 ) unless( defined $repository );
# $repository->get_database->set_debug( 1 );

my $path_info = $repository->get_request->path_info;

# path is one of:
# /$id/ (eprints list of formats)
# /$id/$format/$prettyname (eprints)
# /repository/$format/$prettyname
# /dump/$format/$prettyname
# /subject/$id/$format/$prettyname
# /x-foo/$id/$format/$prettyname

my @path = split( '/', $path_info );
shift @path; # lose leading /

if( $path[0] =~ m/^\d+$/ && scalar @path == 1 )
{
	show_eprint_export_options( $repository, $path[0] );
	exit;
}

my $prettyname = pop @path;
my $format = pop @path;

unless( defined $format )
{
	$repository->not_found;
	exit;
}

my $plugin = $repository->plugin( "Export::$format" );
if( !$plugin )
{
	$repository->not_found;
	exit;
}

if( $plugin->param( "visible" ) eq "staff" )
{
	my $user = basic_auth($repository);
	if (!defined $user) 
	{
		$repository->terminate();
		#$repository->get_request->status( 403 );
		exit;
	}
	if( $user->get_type ne "editor" && $user->get_type ne "admin" )
	{
		$repository->get_request->status( 403 );
		exit;
	}
}

my %arguments = %{$plugin->param( "arguments" )};
# fetch the plugin arguments, if any
foreach my $argname (keys %arguments)
{
	if( defined $repository->param( $argname ) )
	{
		$arguments{$argname} = $repository->param( $argname );
	}
}

if( $path[0] =~ m/^\d+$/ && scalar @path == 1 )
{
	export_dataobj( $repository, $plugin, \%arguments, "eprint", $path[0] );
}
elsif( $path[0] eq "repository" && scalar @path == 1 )
{
	export_rdf_repository( $repository, $plugin, \%arguments );
}
elsif( $path[0] eq "dump" && scalar @path == 1 )
{
	export_rdf_dump( $repository, $plugin, \%arguments );
}
elsif( $path[0] eq "subject" && scalar @path == 2 )
{
	export_subject( $repository, $plugin, \%arguments, $path[1] );
}
elsif( $path[0] eq "records" && scalar @path == 1) 
{
	export_eprints( $repository, $plugin, \%arguments );
}
elsif( $path[1] =~ m/^ext-/ && scalar @path == 2 )
{
	export_rdf_resource( $repository, $plugin, \%arguments, $path[0], $path[1] );
}
elsif( scalar @path == 2 )
{
	export_dataobj( $repository, $plugin, \%arguments, @path[0,1] );
}
elsif( $path[2] eq "contents" ) 
{
	export_contents( $repository, $plugin, \%arguments, @path[0,1] );
}
else
{
	$repository->not_found;
}
	
exit;

sub basic_auth 
{
	my ( $repository ) = @_;
	
	my $user = $repository->current_user;
	my $request = $repository->get_request;
	my $response = EPrints::Sword::Utils::authenticate( $repository, $request );
	if (!defined $user) {
		$user = $response->{owner};
	}
	
	if ($user) {
		return $user;
	}
	
	if (!defined $user) 
	{
		if( defined $response->{error} )
		{
			my $error = $response->{error};
			if( defined $error->{x_error_code} )
			{
				$request->headers_out->{'X-Error-Code'} = $error->{x_error_code};
			}
			if( $error->{no_auth} )
			{
				$request->status( $error->{status_code} );
				$request->err_headers_out->{'WWW-Authenticate'} = 'Basic realm="Authenticate"';
				return undef;
				#return Apache2::Const::DONE;
			}

			my $error_doc = EPrints::Sword::Utils::generate_error_document( $repository, 
					summary => "Authentication error.",
					href => $error->{error_href}
					);

			$request->status( $error->{status_code} );
			$request->headers_out->{'Content-Length'} = length $error_doc;
			$request->content_type('application/atom+xml');
			$request->print( $error_doc );
			$repository->terminate;
		}

		$repository->get_request->status( 403 );
		exit;
	}
}


# Export all eprints owned by logged in user
sub export_eprints 
{	
	my( $repository, $plugin, $args ) = @_;

	my $user = basic_auth($repository);
	if (!defined $user) 
	{
		#$repository->get_request->status( 403 );
		$repository->terminate();
		exit;
	}

	my $list = $user->owned_eprints_list(order=>"datestamp");
	
	$plugin->initialise_fh( \*STDOUT );
	
	$repository->send_http_header( "content_type"=>$plugin->param("mimetype") );
	
	$plugin->output_list( fh=>\*STDOUT, list=>$list );
}


# Dump ALL eprint triples. Not Subject or repository ones, though.
sub export_rdf_dump
{
	my( $repository, $plugin, $args ) = @_;

	if( !$plugin->can_accept( "list/triple" ) )
	{
		$repository->not_found;
		exit;
	}

	my $searchexp = EPrints::Search->new(
			allow_blank => 1,
			dataset => $repository->dataset( "triple" ),
			session => $repository );
	my $list = $searchexp->perform_search;
	$plugin->initialise_fh( \*STDOUT );
	$repository->send_http_header( "content_type"=>$plugin->param("mimetype") );
	$plugin->output_list( fh=>\*STDOUT, list=>$list );
}

sub export_rdf_repository
{
	my( $repository, $plugin, $args ) = @_;

	if( !$plugin->can_accept( "list/triple" ) )
	{
		$repository->not_found;
		exit;
	}

	my $repository_uri = "<".$repository->config( "base_url" )."/id/repository>";

	my $graph = EPrints::RDFGraph->new( repository=>$repository );
	$graph->add( 
		  subject => "<>",
		predicate => "foaf:primaryTopic",
		   object => $repository_uri );
	$graph->add_boilerplate_triples();	
	$graph->add_repository_triples();

	$repository->send_http_header( "content_type"=>$plugin->param("mimetype") );
	$plugin->initialise_fh( \*STDOUT );
	print $plugin->output_graph( $graph );
}

sub export_subject
{
	my( $repository, $plugin, $args, $subject_id ) = @_;

	if( !$plugin->can_accept( "list/subject" ) )
	{
		$repository->not_found;
		exit;
	}

	my $subject = $repository->dataset( "subject" )->dataobj( $subject_id );
	if( !defined $subject )
	{
		$repository->not_found;
		return;
	}

	my $list = $repository->dataset( "subject" )->search( 
		filters => [
			{
				meta_fields => [qw/ ancestors /],
				value => $subject_id,
			}
		],
	);

	$repository->send_http_header( "content_type"=>$plugin->param("mimetype") );
	$plugin->initialise_fh( \*STDOUT );

	print $list->export( $plugin->get_subtype, %{$args} );
}

sub export_rdf_resource
{
	my( $repository, $plugin, $args, $resource_type, $resource_id ) = @_;

	if( !$plugin->can_accept( "list/triple" ) )
	{
		$repository->not_found;
		exit;
	}

	my $uri = "epid:$resource_type/$resource_id";
	my $list = $repository->dataset( "triple" )->search( 
		filters => [
			{
				meta_fields => [qw/ secondary_resource /],
				value => $uri,
			}
		],
	);

	if( !$list->count )
	{
		$repository->not_found;
		exit;
	}

	my $graph = EPrints::RDFGraph->new( repository=>$repository );
	$graph->add( 
		  subject => "<>",
		predicate => "foaf:primaryTopic",
		   object => $uri );
	$graph->add_boilerplate_triples();	

	$list->map( sub{ 
		my( $session, $dataset, $dataobj ) = @_;
		$graph->add_dataobj_triples( $dataobj );
	} );

	$repository->send_http_header( "content_type"=>$plugin->param("mimetype") );
	$plugin->initialise_fh( \*STDOUT );
	
	print $plugin->output_graph( $graph, %{$args} );
}

sub export_dataobj
{
	my( $repository, $plugin, $args, $datasetid, $id ) = @_;

	my $dataset = $repository->dataset( $datasetid );
	$repository->not_found( "No such dataset" ), exit if !defined $dataset;

	if( !$plugin->can_accept( "dataobj/".$dataset->base_id ) )
	{
		$repository->not_found;
		exit;
	}

	my $dataobj = $dataset->dataobj( $id );
	$repository->not_found( "No such dataobj" ), exit if !defined $dataobj;

	if( $dataobj->isa( "EPrints::DataObj::EPrint" ) )
	{
		$dataset = $dataobj->get_dataset;
	}

	my $allowed = 0;

	my $priv = "export";
	if( $dataset->id ne $dataset->base_id )
	{
		$priv = join('/', $dataset->base_id, $dataset->id, $priv );
	}
	else
	{
		$priv = join('/', $dataset->base_id, $priv );
	}

	my $user = basic_auth($repository);
	if( $repository->allow_anybody( $priv ) )
	{
		$allowed = 1;
	}
	else
	{
		exit if !defined $user;
		if(
			$user->allow( "$priv:owner", $dataobj ) ||
			$user->allow( "$priv:editor", $dataobj ) ||
			$user->allow( $priv )
		  )
		{
			$allowed = 1;
		}
	}
	if( $dataobj->isa( "EPrints::DataObj::Document" ) )
	{
		my $r = $repository->get_request();
		my $result = $repository->call( "can_request_view_document", $dataobj, $r );
		if( $result eq "ALLOW" )
		{
			$allowed = 1;
		}
		elsif( $result eq "USER" && defined $user && $dataobj->user_can_view( $user ) )
		{
			$allowed = 1;
		}

	}
	if( !$allowed )
	{
		# 403 Forbidden
		$repository->get_request->status( 403 );
		exit;
	}

	$repository->not_found( "Can't export" ), exit if !$plugin->can_accept( "dataobj/".$dataset->base_id );

	$repository->send_http_header( "content_type"=>$plugin->param("mimetype") );
	$plugin->initialise_fh( \*STDOUT );
	if( my $field = $dataset->get_datestamp_field() )
	{
		my $r = $repository->get_request;
		$r->headers_out->{'Last-Modified'} = Apache2::Util::ht_time(
			$r->pool,
			EPrints::Time::datestring_to_timet( undef, $field->get_value( $dataobj ) )
		);
	}
	print $plugin->output_dataobj( $dataobj, %$args );
}

sub export_contents
{
	my( $repository, $plugin, $args, $datasetid, $id ) = @_;

	my $parent_dataset = $repository->dataset( $datasetid );
	my $dataset = undef;

	if ($parent_dataset->base_id eq "eprint") {
		$dataset = $repository->dataset( "document" );
	} 
#TODO, support file exports from documents.
#elsif ($parent_dataset->base_id eq "document") {
#		$dataset = $repository->dataset( "file" );
#	}

	$repository->not_found( "No such dataset" ), exit if !defined $dataset;

	if( !$plugin->can_accept( "list/".$dataset->base_id ) )
	{
		$repository->not_found;
		exit;
	}

	my $parent_dataobj = $parent_dataset->dataobj( $id );
	$repository->not_found( "No such dataobj" ), exit if !defined $parent_dataobj;

	if( $parent_dataobj->isa( "EPrints::DataObj::EPrint" ) )
	{
		$parent_dataset = $parent_dataobj->get_dataset;
	}
	
	my @items;
	if ($dataset->base_id eq "document") {
		@items = $parent_dataobj->get_all_documents();
	}

	if (!defined @items) {
		$repository->not_found;
		exit;
	}

	my $list = EPrints::List->new(
		session => $repository,
		dataset => $repository->dataset( "document" ),
		ids => [map { $_->id } @items] );	

	my $user = basic_auth($repository);
	if (!defined $user) 
	{
		$repository->terminate();
		#$repository->get_request->status( 403 );
		exit;
	}

	my $allowed = 0;

	my $priv = "export";
	if( $parent_dataset->id ne $parent_dataset->base_id )
	{
		$priv = join('/', $parent_dataset->base_id, $parent_dataset->id, $priv );
	}
	else
	{
		$priv = join('/', $parent_dataset->base_id, $priv );
	}

	if( $repository->allow_anybody( $priv ) )
	{
		$allowed = 1;
	}
	elsif( defined $user )
	{
		if(
			$user->allow( "$priv:owner", $parent_dataobj ) ||
			$user->allow( "$priv:editor", $parent_dataobj ) ||
			$user->allow( $priv )
		  )
		{
			$allowed = 1;
		}
	}

	if( !$allowed )
	{
		# 403 Forbidden
		$repository->get_request->status( 403 );
		exit;
	}

	$repository->not_found( "Can't export" ), exit if !$plugin->can_accept( "list/".$dataset->base_id );

	$repository->send_http_header( "content_type"=>$plugin->param("mimetype") );
	$plugin->initialise_fh( \*STDOUT );
	print $plugin->output_list( list=>$list, %$args );
}

sub show_eprint_export_options
{
	my( $repository, $eprintid ) = @_;

	my $eprint = $repository->eprint( $eprintid );
	if( !defined $eprint )
	{
		$repository->not_found;
		exit;
	}

	my $title = $repository->html_phrase( "cgi/export:title",
			shortname=>$eprint->render_description );
	my $page = $repository->html_phrase( "cgi/export:page",
		citation => $eprint->render_citation_link,
		formats => $eprint->render_export_links ); 
	$repository->build_page( $title, $page, "export" );
	$repository->send_page;
}
