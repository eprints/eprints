######################################################################
#
#  EPrints Repository Info Exporter
#
######################################################################
#
#  __COPYRIGHT__
#
# Copyright 2000-2010 University of Southampton. All Rights Reserved.
# 
#  __LICENSE__
#
######################################################################

use EPrints;
use Data::Dumper;

use strict;
my $repository = EPrints->new->current_repository;
exit( 0 ) unless( defined $repository );
# $repository->get_database->set_debug( 1 );

my $path_info = $repository->get_request->path_info;

# path is one of:
# /$id/ (eprints list of formats)
# /$id/$format/$prettyname (eprints)
# /repository/$format/$prettyname
# /subject/$id/$format/$prettyname
# /x-foo/$id/$format/$prettyname

my @path = split( '/', $path_info );
shift @path; # lose leading /

if( $path[0] =~ m/^\d+$/ && scalar @path == 1 )
{
	show_eprint_export_options( $repository, $path[0] );
	exit;
}

my $prettyname = pop @path;
my $format = pop @path;

unless( defined $format )
{
	$repository->not_found;
	exit;
}

my $plugin = $repository->plugin( "Export::$format" );
if( !$plugin )
{
	$repository->not_found;
	exit;
}

my %arguments = %{$plugin->param( "arguments" )};
# fetch the plugin arguments, if any
foreach my $argname (keys %arguments)
{
	if( defined $repository->param( $argname ) )
	{
		$arguments{$argname} = $repository->param( $argname );
	}
}

if( $path[0] =~ m/^\d+$/ && scalar @path == 1 )
{
	export_eprint( $repository, $plugin, \%arguments, $path[0] );
}
elsif( $path[0] eq "repository" && scalar @path == 1 )
{
	export_rdf_repository( $repository, $plugin, \%arguments );
}
elsif( $path[0] eq "subject" && scalar @path == 2 )
{
	export_subject( $repository, $plugin, \%arguments, $path[1] );
}
elsif( $path[0] =~ s/^x-// && scalar @path == 2 )
{
	export_rdf_resource( $repository, $plugin, \%arguments, $path[0], $path[1] );
}
elsif( scalar @path == 2 )
{
	export_dataobj( $repository, $plugin, \%arguments, @path[0,1] );
}
else
{
	$repository->not_found;
}
	
exit;



sub export_rdf_repository
{
	my( $repository, $plugin, $args ) = @_;

	if( !$plugin->can_accept( "list/triple" ) )
	{
		$repository->not_found;
		exit;
	}

	my $repository_uri = "<".$repository->config( "base_url" )."/id/repository>";

	my $graph = EPrints::RDFGraph->new( repository=>$repository );
	$graph->add( 
		  subject => "<>",
		predicate => "foaf:primaryTopic",
		   object => $repository_uri );
	$graph->add_boilerplate_triples();	
	$graph->add_trigger_triples( "repository" );

	$repository->send_http_header( "content_type"=>$plugin->param("mimetype") );
	$plugin->initialise_fh( \*STDOUT );
	print $plugin->output_graph( $graph );
}

sub export_eprint
{
	my( $repository, $plugin, $args, $eprint_id ) = @_;

	if( !$plugin->can_accept( "dataobj/eprint" ) )
	{
		$repository->not_found;
		exit;
	}
	$repository->send_http_header( "content_type"=>"text/plain" );
	my $eprint = $repository->dataset( "archive" )->dataobj( $eprint_id );
	if( !defined $eprint )
	{
		$repository->not_found;
		return;
	}

	$repository->send_http_header( "content_type"=>$plugin->param("mimetype") );
	$plugin->initialise_fh( \*STDOUT );

	print $eprint->export( $plugin->get_subtype, %{$args} );
}

sub export_subject
{
	my( $repository, $plugin, $args, $subject_id ) = @_;

	if( !$plugin->can_accept( "list/subject" ) )
	{
		$repository->not_found;
		exit;
	}

	my $subject = $repository->dataset( "subject" )->dataobj( $subject_id );
	if( !defined $subject )
	{
		$repository->not_found;
		return;
	}

	my $list = $repository->dataset( "subject" )->search( 
		filters => [
			{
				meta_fields => [qw/ ancestors /],
				value => $subject_id,
			}
		],
	);

	$repository->send_http_header( "content_type"=>$plugin->param("mimetype") );
	$plugin->initialise_fh( \*STDOUT );

	print $list->export( $plugin->get_subtype, %{$args} );
}

sub export_rdf_resource
{
	my( $repository, $plugin, $args, $resource_type, $resource_id ) = @_;

	if( !$plugin->can_accept( "list/triple" ) )
	{
		$repository->not_found;
		exit;
	}

	my $uri = "epid:x-$resource_type/$resource_id";
	my $list = $repository->dataset( "triple" )->search( 
		filters => [
			{
				meta_fields => [qw/ secondary_resource /],
				value => $uri,
			}
		],
	);

	my $graph = EPrints::RDFGraph->new( repository=>$repository );
	$graph->add( 
		  subject => "<>",
		predicate => "foaf:primaryTopic",
		   object => $uri );
	$graph->add_boilerplate_triples();	

	$list->map( sub{ 
		my( $session, $dataset, $dataobj ) = @_;
		$graph->add_dataobj_triples( $dataobj );
	} );

	$repository->send_http_header( "content_type"=>$plugin->param("mimetype") );
	$plugin->initialise_fh( \*STDOUT );
	
	print $plugin->output_graph( $graph, %{$args} );
}

sub export_dataobj
{
	my( $repository, $plugin, $args, $datasetid, $id ) = @_;

	my $dataset = $repository->dataset( $datasetid );
	$repository->not_found, exit if !defined $dataset;

	if( $datasetid eq "eprint" )
	{
		return export_eprint( $repository, $plugin, $args, $id );
	}
	elsif( $datasetid eq "subject" )
	{
		return export_subject( $repository, $plugin, $args, $id );
	}

	my $priv = "$datasetid/view";
	if( !$repository->allow_anybody( $priv ) )
	{
		# perhaps should be ACCESS DENIED?
		$repository->not_found;
		exit;
	}

	$repository->not_found, exit if !$plugin->can_accept( "dataobj/".$dataset->base_id );

	my $dataobj = $dataset->dataobj( $id );
	$repository->not_found, exit if !defined $dataobj;

	$repository->send_http_header( "content_type"=>$plugin->param("mimetype") );
	$plugin->initialise_fh( \*STDOUT );
	print $plugin->output_dataobj( $dataobj );
}

sub show_eprint_export_options
{
	my( $repository, $eprintid ) = @_;

	my $eprint = $repository->eprint( $eprintid );
	if( !defined $eprint )
	{
		$repository->not_found;
		exit;
	}

	my $title = $repository->html_phrase( "cgi/export:title",
			shortname=>$eprint->render_description );
	my $page = $repository->html_phrase( "cgi/export:page",
		citation => $eprint->render_citation_link,
		formats => $eprint->render_export_links ); 
	$repository->build_page( $title, $page, "export" );
	$repository->send_page;
}
